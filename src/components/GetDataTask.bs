import "pkg:/source/roku_modules/rokurequests/Requests.brs"

sub init()
    m.top.functionName = "execute"
end sub

const EndpointUrl = "https://raw.githubusercontent.com/arturocuya/notflix/master/movies-data.json"

sub execute()
    response = rokurequests_Requests().get(EndpointUrl, {
        timeout: 1000,
        useCache: true
    })

    content = CreateObject("roSGNode", "ContentNode")

    if (response.statusCode <> 200)
        ? "[GetDataTask] status code not ok", response.statusCode
        m.top.output = content
        return
    end if

    category = LCase(m.top.inputCategory)
    categoryMovies = response.json[category]

    if (categoryMovies = invalid or categoryMovies.Count() = 0)
        ? "[GetDataTask] category empty", category
        m.top.output = content
        return
    end if

    row = content.CreateChild("ContentNode")

    for each movie in categoryMovies
        rowItem = row.CreateChild("ContentNode")
        rowItem.addFields({
            title: movie.title,
            director: movie.director,
            thumbnailUrl: movie.thumbnailUrl,
            frameUrl: movie.frameUrl
        })
    end for

    m.top.output = content
end sub

