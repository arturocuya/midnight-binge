import "pkg:/source/roku_modules/rokurequests/Requests.brs"
import "pkg:/source/roku_modules/promises/promises.brs"

sub init()
    m.top.functionName = "execute"
end sub

const EndpointUrl = "https://raw.githubusercontent.com/arturocuya/notflix/master/movies-data.json"

function getPromiseAndRun(category as string) as object
    m.promise = promises.create()

    m.category = category
    m.top.control = "RUN"

    return m.promise
end function

sub execute()
    response = rokurequests_Requests().get(EndpointUrl, {
        timeout: 1000,
        useCache: true
    })

    content = CreateObject("roSGNode", "ContentNode")

    if (response.statusCode <> 200)
        ? "[GetDataTask] status code not ok", response.statusCode
        promises.resolve(content, m.promise)
        return
    end if

    category = LCase(m.category)
    categoryMovies = response.json[category]

    if (categoryMovies = invalid or categoryMovies.Count() = 0)
        ? "[GetDataTask] category empty", category
        promises.resolve(content, m.promise)
        return
    end if

    row = content.CreateChild("ContentNode")

    for each movie in categoryMovies
        rowItem = row.CreateChild("ContentNode")
        rowItem.addFields({
            title: movie.title,
            director: movie.director,
            thumbnailUrl: movie.thumbnailUrl,
            frameUrl: movie.frameUrl
        })
    end for

    promises.resolve(content, m.promise)
end sub

