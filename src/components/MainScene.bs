enum MovieCategory
    Action = "Action"
    Comedy = "Comedy"
    Horror = "Horror"
    Drama = "Drama"
    Animation = "Animation"
end enum

sub init()
    m.categories = m.top.CreateChild("ButtonGroup")
    m.categories.buttons = [
        MovieCategory.Action,
        MovieCategory.Comedy,
        MovieCategory.Horror,
        MovieCategory.Drama,
        MovieCategory.Animation
    ]

    m.categories.id = "categories"
    m.categories.translation = [96, 205]

    m.categories.observeField("buttonSelected", "handleCategoryButtonSelected")

    m.thumbnails = m.top.findNode("thumbnails")

    m.getDataTask = CreateObject("roSGNode", "GetDataTask")
    m.getDataTask.observeField("output", "handleGetDataTaskOutput")

    m.categories.SetFocus(true)

    loadCategory(MovieCategory.Action)
end sub

sub loadCategory(category as string)
    m.getDataTask.inputCategory = category
    m.getDataTask.control = "RUN"
end sub

sub handleCategoryButtonSelected()
    buttonSelected = m.categories.buttonSelected
    category = m.categories.buttons[buttonSelected]

    loadCategory(category)
end sub

sub handleGetDataTaskOutput()
    output = m.getDataTask.output
    m.thumbnails.content = output
end sub

function onKeyEvent(key as string, pressed as boolean) as boolean
    if (not pressed)
        return false
    end if

    if (key = "right")
        m.thumbnails.SetFocus(true)
    else if (key = "left")
        m.categories.SetFocus(true)
    end if

    return false
end function
